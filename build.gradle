plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.hidetake.ssh' version '2.10.1'
}

group = 'net.bancino.robotics'
version = '1.0.2'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    jcenter()
    /* WPILib */
    maven {
        url 'https://first.wpi.edu/FRC/roborio/maven/release/'
    }
}

dependencies {
    /* Declare dependencies here */
    api 'edu.wpi.first.ntcore:ntcore-java:+'

    /* Use JUnit Jupiter API for testing. */
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'JLimelight'
            from components.java
        }
    }
    repositories {
        maven {
            url = "$buildDir/repo"
        }
    }
}

remotes {
    webServer {
        host = 'bancino.net'
        if (project.hasProperty('sshUser')) {
            user = project.getProperty('sshUser')
        }
        if (project.hasProperty('sshPass')) {
            password = project.getProperty('sshPass')
        }
    }
}

task deployMaven {
    dependsOn publish
    doLast {
        ssh.run {
            session(remotes.webServer) {
                put from: "$buildDir/repo/net", into: '/var/www/dump/maven'
            }
        }
    }
}
